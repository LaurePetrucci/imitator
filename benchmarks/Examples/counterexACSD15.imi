(************************************************************
 *                         IMITATOR
 *
 * Counter-example given by a reviewer of ACSD 2015
 *
 * Author          : "Reviewer 1" (input by Etienne Andre)
 *
 * Created         : 2015/03/26
 * Last modified   : 2015/05/06
 *
 * IMITATOR version: 2.7-beta2
 ************************************************************)

var 	x, y
		: clock;

	p
		: parameter;


(*
	l1 -( x == 1, x := 0 )-> l2
	l2 -( y == 1, y := 0 )-> l3
	l3 -( y <= p, y := 0 )-> l1
	l1 -( y == 0 /\ x >= 1 )-> l4
*)


(************************************************************)
  automaton pta
(************************************************************)
synclabs: ;

loc l1: while True wait {}
	when x = 1 do {x' = 0} goto l2;
	when y = 0 & x >= 1 goto l4;

loc l2: while True wait {}
	when y = 1 do {y' = 0} goto l3;

loc l3: while True wait {}
	when y <= p do {y' = 0} goto l1;

loc l4: while True wait {}

end (*a*)



(************************************************************)
(* Analysis *)
(************************************************************)

init := True
	(*------------------------------------------------------------
	   INITIAL LOCATION
	  ------------------------------------------------------------*)
	& loc[pta] = l1

	(*------------------------------------------------------------
	   INITIAL CLOCKS
	  ------------------------------------------------------------*)
	& x = 0
	& y = 0

	(*------------------------------------------------------------
	   PARAMETER CONSTRAINTS
	  ------------------------------------------------------------*)
	& p >= 0
	& p <= 1
;

property := unreachable loc[pta] = l4;

end

