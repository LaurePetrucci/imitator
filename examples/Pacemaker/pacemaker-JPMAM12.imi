(************************************************************
 *                         IMITATOR
 *
 * Model of a pacemaker
 *
 * "Modeling and Verification of a Dual Chamber Implantable Pacemaker"
 * Zhihao Jiang, Miroslav Pajic, Salar Moarref, Rajeev Alur, Rahul Mangharam
 * TACAS 2014
 *
 * Author          : Etienne Andre
 * Created         : 2015/02/11
 * Last modified   : 2015/02/11
 * IMITATOR version: 2.6.2
 ************************************************************)

var 
	(* local clocks *)
	t_AVI, t_LRI, t_PVARP, t_VRP,
	(* global clocks *)
	clk, x, x_urgent
		: clock;

	TURI,
	TLRI,
	TAVI,
	TPVAB,
	TPVARP,
	TVRP,
	Aminwait,
	Amaxwait,
	
	(* Constants for readability *)
	TRUE = 1,
	FALSE = 0
	
		: parameter;
	
	(* Hack to test another PTA's location *)
	AVIisIdle, PVARPisIdle
		: discrete;



(************************************************************)
  automaton LRI
(************************************************************)
synclabs: AP, AS, VP, VS;

loc LRI: while t_LRI <= TLRI - TAVI wait {}
	when True sync VP do {t_LRI' = 0} goto LRI;
	when True sync VS do {t_LRI' = 0} goto LRI;
	when t_LRI >= TLRI - TAVI sync AP do {t_LRI' = 0} goto LRI;
	when True sync AS goto ASed;

loc ASed: while True wait {}
	when True sync VP do {t_LRI' = 0} goto LRI;
	when True sync VS do {t_LRI' = 0} goto LRI;

end (*LRI*)



(************************************************************)
  automaton AVI
(************************************************************)
synclabs: AP, AS, VP, VS;

loc Idle: while True wait {}
	when True sync AP do {t_AVI' = 0, AVIisIdle' = FALSE} goto AVI;
	when True sync AS do {t_AVI' = 0, AVIisIdle' = FALSE} goto AVI;

loc AVI: while t_AVI <= TAVI wait {}
	when True sync VS goto Idle;
	when t_AVI >= TAVI & clk >= TURI sync VP do {AVIisIdle' = TRUE} goto Idle;
	when t_AVI >= TAVI & clk < TURI sync VP goto WaitURI;

loc WaitURI: while clk <= TURI wait {}
	when True sync VS do {AVIisIdle' = TRUE} goto Idle;
	when clk >= TURI sync VP do {AVIisIdle' = TRUE} goto Idle;


end (*AVI*)



(************************************************************)
  automaton URI
(************************************************************)
synclabs: VP, VS;

loc URI: while True wait {}
	when True sync VP do {clk' = 0} goto URI;
	when True sync VS do {clk' = 0} goto URI;

end (*URI*)



(************************************************************)
  automaton PVARP
(************************************************************)
synclabs: AR, AS, Aget, VP, VS, PVARPbacktoIdle;

loc Idle: while True wait {}
	when True sync VP do {t_PVARP' = 0, PVARPisIdle' = FALSE} goto PVAB;
	when True sync VS do {t_PVARP' = 0, PVARPisIdle' = FALSE} goto PVAB;
	when True sync Aget do {t_PVARP' = 0, PVARPisIdle' = FALSE} goto inter;

(* ------------------------------------------------------------ *)
(** COMMITTED **)
(*loc inter: while True wait {}
	when True sync AS goto Idle;*)

(* Replaced with the following gadget *)

loc inter: while t_PVARP = 0 wait {}
	(* Easy case: loc[AVI] = idle: committed is urgent *)
	when AVIisIdle = TRUE sync AS do {PVARPisIdle' = TRUE} goto Idle;
	(* Less easy case: loc[AVI] =!= idle: wait! *)
	when AVIisIdle = FALSE goto Wait;

loc Wait: while True wait {}
	when True sync VP do {t_PVARP' = 0} goto Urgent;
	when True sync VS do {t_PVARP' = 0} goto Urgent;

loc Urgent: while t_PVARP = 0 wait {}
	when True sync AS do {PVARPisIdle' = TRUE} goto Idle;

(* END gadget for committed *)
(* ------------------------------------------------------------ *)

loc PVAB: while t_PVARP <= TPVAB wait {}
	when t_PVARP >= TPVAB goto PVARP;

loc PVARP: while t_PVARP <= TPVARP wait {}
	when True sync Aget do {x_urgent' = 0} goto inter1;
	when t_PVARP >= TPVARP do {PVARPisIdle' = TRUE} sync PVARPbacktoIdle goto Idle;

(** COMMITTED **)
(* In fact, urgent is OK because AR is not synchronized with anyone else *)
loc inter1: while x_urgent = 0 wait {}
	when x_urgent = 0 sync AR goto PVARP;


end (*PVARP*)



(************************************************************)
  automaton VRP
(************************************************************)
synclabs: AP, AS, Aget, Vget, VP, VS, PVARPbacktoIdle;

loc Idle: while True wait {}
	when True sync Vget do {t_VRP' = 0} goto inter;
	when True sync VP do {t_VRP' = 0} goto VRP;
	(* To prevent blocking *)
	when True sync AP goto Idle;
	when True sync AS goto Idle;
	when True sync Aget goto Idle;

(* ------------------------------------------------------------ *)
(** COMMITTED **)
(*loc inter: while True wait {}
	when True sync VS do {t_VRP' = 0} goto VRP;*)

loc inter: while t_VRP = 0 wait {}
	(* Ready *)
	when AVIisIdle = FALSE & PVARPisIdle = TRUE sync VS goto VRP;
	(* Not ready...*)
	when AVIisIdle = TRUE & PVARPisIdle = TRUE goto bNOKdOK;
	when AVIisIdle = FALSE & PVARPisIdle = FALSE goto bOKdNOK;
	when AVIisIdle = TRUE & PVARPisIdle = FALSE goto bNOKdNOK;

(* AVI(b) not ready; PVARP(d) not ready *)
loc bNOKdNOK: while True wait {}
	when True sync AP goto bOKdNOK;
	when True sync AS do {t_VRP' = 0} goto bOKdOK;
	when True sync PVARPbacktoIdle goto bNOKdOK;
	(* To prevent blocking *)
	when True sync Aget goto bNOKdNOK;

(* AVI(b) ready; PVARP(d) not ready *)
loc bOKdNOK: while True wait {}
	when True sync PVARPbacktoIdle do {t_VRP' = 0} goto bOKdOK;
	(* To prevent blocking *)
	when True sync Aget goto bOKdNOK;

(* AVI(b) not ready; PVARP(d) ready *)
loc bNOKdOK: while True wait {}
	when True sync AP do {t_VRP' = 0} goto bOKdOK;
	when True sync Aget goto bNOKdNOK2;

(* AVI(b) not ready; PVARP(d) not ready *)
loc bNOKdNOK2: while True wait {}
	when True sync AS do {t_VRP' = 0} goto bOKdOK;

(* AVI(b) ready; PVARP(d) ready: fire! *)
loc bOKdOK: while t_VRP = 0 wait {}
	when t_VRP = 0 sync VS goto VRP;

(* END gadget for committed *)
(* ------------------------------------------------------------ *)

loc VRP: while t_VRP <= TVRP wait {}
	when t_VRP >= TVRP goto Idle;
	(* To prevent blocking *)
	when True sync AP goto VRP;
	when True sync AS goto VRP;
	when True sync Aget goto VRP;


end (*VRP*)



(************************************************************)
  automaton RHM
(************************************************************)
synclabs: Aget, AP;

loc AReady: while x < Amaxwait wait {}
	when True sync AP do {x' = 0} goto AReady;
	when x > Aminwait sync Aget do {x' = 0} goto AReady;

end (*RHM*)


(************************************************************)
(* Analysis *)
(************************************************************)

init := True
	(*------------------------------------------------------------
	   INITIAL LOCATION
	  ------------------------------------------------------------*)
	& loc[LRI]			= LRI
	& loc[AVI]			= Idle
	& loc[URI]			= URI
	& loc[PVARP]		= Idle
	& loc[VRP]			= Idle
	& loc[RHM]			= AReady


	(*------------------------------------------------------------
	   INITIAL DISCRETE
	  ------------------------------------------------------------*)
	& AVIisIdle			= TRUE
	& PVARPisIdle		= TRUE
	
	(*------------------------------------------------------------
	   INITIAL CLOCKS
	  ------------------------------------------------------------*)
	& clk		= 0
	& x			= 0
	& x_urgent	= 0
	
	& t_AVI		= 0
	& t_LRI		= 0
	& t_PVARP	= 0
	& t_VRP		= 0

	(*------------------------------------------------------------
	   PARAMETER CONSTRAINTS
	  ------------------------------------------------------------*)
	& TAVI		>= 0
	& TLRI		>= 0
	& TPVARP	>= 0
	& TVRP		>= 0
	& TURI		>= 0
	& TPVAB		>= 0
	& Aminwait	>= 0
	& Amaxwait	>= 0

	(*------------------------------------------------------------
	   PARAMETER VALUATION
	  ------------------------------------------------------------*)
	& TAVI		= 150
	& TLRI		= 1000
	& TPVARP	= 100
	& TVRP		= 150
	& TURI		= 400
	& TPVAB		= 50
;

end
